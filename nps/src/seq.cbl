 IDENTIFICATION DIVISION.
 PROGRAM-ID.
     SEQ.
************************************************
*                                              *
* THIS PROGRAM READS A COBOL PROGRAM 'INP.FIL' *
* WITH SEQUENCE NUMBERS AND WRITES A COBOL     *
* PROGRAM 'PRT.FIL' WITHOUT SEQUENCE NUMBERS   *
* IT DEMONSTRATES THE USE OF VARIABLE LENGTH   *
* RECORD FILES IN NPS MICRO-COBOL.             *
*                                              *
************************************************

 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SOURCE-COMPUTER.
     8080.
 OBJECT-COMPUTER.
     8080.
 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
     SELECT PRINT-FILE ASSIGN
     PRT.FIL.
     SELECT INPUT-FILE ASSIGN INP.FIL.
 DATA DIVISION.
 FILE SECTION.
 FD  PRINT-FILE
     RECORD 6 TO 80
* RECORD 6 TO 80 INDICATES THIS FILE IS 
* VARIABLE LENGTH.
     LABEL RECORDS
     OMITTED.
 01 OUTPUT-LINE PIC X(80).

 FD INPUT-FILE
    RECORD 6 TO 80
    LABEL RECORDS OMITTED.
 01 INPUT-LINE.
   02 SEQ-NUMBER PIC X(6).
   02 INFO       PIC X(74).

 WORKING-STORAGE SECTION.
 01 END-OF-FILE PIC X VALUE 'F'.

 PROCEDURE DIVISION.
 BEGIN-PARA.
   OPEN OUTPUT PRINT-FILE.
   OPEN INPUT INPUT-FILE.
   READ INPUT-FILE END MOVE 'T' TO END-OF-FILE.
   PERFORM PROCESS UNTIL END-OF-FILE EQUAL 'T'.
   CLOSE PRINT-FILE.
   CLOSE INPUT-FILE.
   STOP RUN.
 
 PROCESS.
   MOVE INFO TO OUTPUT-LINE.
   WRITE OUTPUT-LINE.
   READ INPUT-FILE END MOVE 'T' TO END-OF-FILE.
